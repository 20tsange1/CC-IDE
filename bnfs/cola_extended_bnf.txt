<contract> 
::= <component>
 | <component> C-AND <contract>

<component> 
::= <definition>
 | <conditional-definition>
 | <statement>
 | <conditional-statement>

<definition> 
::= <simple-definition>
 | <simple-definition> AND <definition>

<simple-definition> 
::= <ID> <subject> IS <subject>
 | <ID> <subject> EQUALS <numerical-expression>

<numerical-expression> 
L::= <num>
 | <numerical-object>
 | <numerical-expression> <operator> <numerical-expression>

<operator> 
::= PLUS 
 | MINUS 
 | TIMES 
 | DIVIDE

<conditional-definition> 
::= <definition> IF <condition>
 | IF <condition> THEN <definition>
 | <definition> IF <condition> ELSE <definition>
 | IF <condition> THEN <definition> ELSE <definition>

<statement> 
::= <simple-statement>
 | <simple-statement> OR <statement>
 | <simple-statement> AND <statement>

<conditional-statement> 
::= <statement> IF <condition>
 | IF <condition> THEN <statement>
 | <statement> IF <condition> ELSE <statement>
 | IF <condition> THEN <statement> ELSE <statement>

<simple-statement> 
::= <ID> <holds>? <subject> <modal-verb> <verb> <object> <receiver> <date>
 | <ID> <holds>? <subject> <date> <modal-verb> <verb> <object> <receiver>
 | <ID> <holds>? <date> <subject> <modal-verb> <verb> <object> <receiver>
 | <ID> <holds>? <subject> <verb-status> <object> <receiver> <date>

<condition> 
::= <simple-condition>
 | <simple-condition> OR <condition>
 | <simple-condition> AND <condition>

<simple-condition> 
::= <ID> <holds>? <subject> <verb-status> <object> <receiver> <date>
 | <ID> <holds>? <subject> <date> <verb-status> <object> <receiver>
 | <ID> <holds>? <date> <subject> <verb-status> <object> <receiver>
 | <ID> <holds>? <subject> <modal-verb> <verb> <object> <receiver> <date>
 | <ID> <holds>? <boolean-expression>

<boolean-expression> 
::= <subject> <verb-status> <comparison> <subject>

<ID> 
::= [ <num> ] 
 | [ <num> ( <num> ) ]

<holds> 
::= it is the case that 
 | it is not the case that

<subject> 
::= <string>

<verb> 
::= deliver 
 | pay 
 | charge 
 | refund

<verb-status> 
::= delivered 
 | paid 
 | charged 
 | refunded

<comparison> 
::= less than 
 | <equal> 
 | <more-than>

<equal> 
::= equals 
 | equal to

<more-than> 
::= more than 
 | greater than

<modal-verb> 
::= <obligation> 
 | may 
 | is forbidden to

<obligation> 
::= shall 
 | must

<date> 
::= <specific-date> 
 | on ANYDATE 
 | on SOMEDATE <subject>
 | on THEDATE <subject>
 | <temporal-quantifier> <num> <month> <num>
 | <temporal-quantifier> SOMEDATE <subject>
 | <temporal-quantifier> THEDATE <subject>
 | <temporal-offset> <temporal-quantifier> SOMEDATE <subject>
 | <temporal-offset> <temporal-quantifier> THEDATE <subject>
 | <temporal-quantifier> <temporal-offset> <temporal-quantifier> SOMEDATE <subject>
 | <temporal-quantifier> <temporal-offset> <temporal-quantifier> THEDATE <subject>

<temporal-quantifier> 
::= before 
 | after

<specific-date> 
::= on the <num> <month> <num> 
 | on <num> <month> <num>

<temporal-offset> 
::= <num> day 
 | <num> week 
 | <num> year 
 | <num> days 
 | <num> weeks 
 | <num> years

<month> 
::= January 
 | February 
 | March 
 | April 
 | May 
 | June 
 | July 
 | August 
 | September 
 | October 
 | November 
 | December

<object> 
::= <numerical-object> 
 | <nonnumerical-object>

<numerical-object> 
::= <pounds> <num> 
 | <dollars> <num> 
 | <euros> <num> 
 | AMOUNT <subject>

<nonnumerical-object> 
::= SOMECURRENCY <string> 
 | REPORT <string> 
 | NAMEDOBJECT <string> 
 | OTHEROBJECT <string>

<pounds> 
::= GBP 
 | POUNDS 
 | quid

<dollars> 
::= USD 
 | DOLLARS 
 | buck

<euros> 
::= EUR 
 | EUROS

<receiver> 
::= to <subject>

<string> ::= /[a-zA-Z]+/

<num> ::= /[0-9]+/
